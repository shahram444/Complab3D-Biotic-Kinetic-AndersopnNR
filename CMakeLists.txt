cmake_minimum_required(VERSION 3.5)
#set the project name
project(complab)
enable_language(CXX)
set(EXECUTABLE_NAME "complab")
set (CMAKE_RUNTIME_OUTPUT_DIRECTORY "../")
#if(NOT CMAKE_BUILD_TYPE)
    #set(CMAKE_BUILD_TYPE "Release")
    #set(CMAKE_CONFIGURATION_TYPES "Debug;Release")
#endif()
if(NOT CMAKE_BUILD_TYPE)
    #set(CMAKE_BUILD_TYPE "Debug")
    set(CMAKE_BUILD_TYPE "Release")
endif()
message("Generated with config types: ${CMAKE_CONFIGURATION_TYPES}")
message(${CMAKE_BUILD_TYPE})
# Compiler flags
# Append flags: set(CMAKE_XXX_FLAGS "${CMAKE_XXX_FLAGS} ...")
if(${CMAKE_CXX_COMPILER_ID} STREQUAL GNU)
    message("GCC.")
    set(CMAKE_CXX_FLAGS "-std=c++11 -Wall -Wnon-virtual-dtor")
    set(CMAKE_CXX_FLAGS_RELEASE "-O3 -DNDEBUG")
    set(CMAKE_CXX_FLAGS_DEBUG "-g -DPLB_DEBUG -O0")
elseif(${CMAKE_CXX_COMPILER_ID} STREQUAL Clang)
    message("Clang.")
    set(CMAKE_CXX_FLAGS "-std=c++11 -Wall -Wnon-virtual-dtor")
    set(CMAKE_CXX_FLAGS_RELEASE "-O3 -DNDEBUG")
    set(CMAKE_CXX_FLAGS_DEBUG "-g -DPLB_DEBUG -O0 -faddress-sanitizer -O1 -fno-omit-frame-pointer")
elseif(${CMAKE_CXX_COMPILER_ID} STREQUAL MSVC)
    message("MSVC.")
    set(CMAKE_CXX_FLAGS_RELEASE "/Ox /Ot /GS- /GL /DNDEBUG")
    set(CMAKE_CXX_FLAGS_DEBUG "/DPLB_DEBUG")
    set(CMAKE_EXE_LINKER_FLAGS_RELEASE "/LTCG /INCREMENTAL:NO /OPT:REF")
else()
    message( FATAL_ERROR "CXX compiler not recognized. CMake will quit." )
endif()
option(ENABLE_MPI "Enable MPI" ON)
if(ENABLE_MPI)
    message("Enabling MPI")
    find_package(MPI REQUIRED)
    if(MPI_CXX_FOUND)
        #set(CMAKE_CXX_COMPILER "${MPI_CXX_COMPILER}")
        include_directories(${MPI_CXX_INCLUDE_PATH})
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${MPI_CXX_COMPILE_FLAGS}")
        add_definitions(-DPLB_MPI_PARALLEL)
    endif()
endif()
if(WIN32)
    option(ENABLE_POSIX "Enable POSIX" OFF)
else()
    option(ENABLE_POSIX "Enable POSIX" ON)
endif()
if(ENABLE_POSIX)
    message("Enabling POSIX")
    add_definitions(-DPLB_USE_POSIX)
endif()
if(APPLE)
    add_definitions(-DPLB_MAC_OS_X)
endif()
if(WIN32 OR CYGWIN)
    add_definitions(-DPLB_WINDOWS)
endif()
###############################################################################
# Palabos Library
###############################################################################
# Set PALABOS_ROOT via -DPALABOS_ROOT=/path/to/palabos or environment variable
# Default: look for palabos in versionControl/ subdirectory
if(NOT PALABOS_ROOT)
    if(DEFINED ENV{PALABOS_ROOT})
        set(PALABOS_ROOT $ENV{PALABOS_ROOT})
    elseif(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/versionControl/palabos-v2.3.0")
        set(PALABOS_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/versionControl/palabos-v2.3.0")
    else()
        message(FATAL_ERROR "PALABOS_ROOT is not set. Pass -DPALABOS_ROOT=/path/to/palabos-v2.3.0 or set the PALABOS_ROOT environment variable.")
    endif()
endif()
message("Using Palabos from: ${PALABOS_ROOT}")

include_directories("/usr/local/include")
include_directories("${PALABOS_ROOT}/src")
include_directories("${PALABOS_ROOT}/externalLibraries")
include_directories("${PALABOS_ROOT}/externalLibraries/Eigen3")

link_directories("/usr/local/lib")

option(REBUILD_PALABOS "Rebuild Palabos library from source (OFF = use pre-built)" OFF)

if(REBUILD_PALABOS)
    message("Building Palabos from source...")
    file(GLOB_RECURSE PALABOS_SRC "${PALABOS_ROOT}/src/*.cpp")
    file(GLOB_RECURSE EXT_SRC "${PALABOS_ROOT}/externalLibraries/tinyxml/*.cpp")
    add_library(palabos STATIC ${PALABOS_SRC} ${EXT_SRC})
else()
    message("Using pre-built Palabos library (use -DREBUILD_PALABOS=ON to rebuild)")
    # Look for pre-built library in build directory
    if(WIN32)
        set(PALABOS_LIB_NAME "palabos.lib")
        if(NOT EXISTS "${CMAKE_CURRENT_BINARY_DIR}/${PALABOS_LIB_NAME}")
            set(PALABOS_LIB_NAME "libpalabos.a")  # MinGW uses .a
        endif()
    else()
        set(PALABOS_LIB_NAME "libpalabos.a")
    endif()

    set(PALABOS_LIB_PATH "${CMAKE_CURRENT_BINARY_DIR}/${PALABOS_LIB_NAME}")
    if(NOT EXISTS "${PALABOS_LIB_PATH}")
        message(FATAL_ERROR
            "Pre-built Palabos library not found at ${PALABOS_LIB_PATH}.\n"
            "Run cmake with -DREBUILD_PALABOS=ON first to build it, then use -DREBUILD_PALABOS=OFF for fast rebuilds.")
    endif()

    add_library(palabos STATIC IMPORTED)
    set_target_properties(palabos PROPERTIES IMPORTED_LOCATION "${PALABOS_LIB_PATH}")
endif()
###############################################################################
add_executable(${EXECUTABLE_NAME} "./src/${EXECUTABLE_NAME}.cpp")
# Link with the following libraries
target_link_libraries(${EXECUTABLE_NAME} palabos)
#target_link_libraries(${EXECUTABLE_NAME} glpk)
#target_link_libraries(${EXECUTABLE_NAME} python3.6m)
if(ENABLE_MPI)
    target_link_libraries(${EXECUTABLE_NAME} ${MPI_CXX_LIBRARIES})
endif()
